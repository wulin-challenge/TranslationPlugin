import org.apache.tools.ant.filters.EscapeUnicode

buildscript {
    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }
        mavenCentral()
    }
}

plugins {
    // https://github.com/JetBrains/gradle-intellij-plugin
    id "org.jetbrains.intellij" version "0.7.2"
    id "org.jetbrains.kotlin.jvm" version "1.4.10"
}

repositories {
    maven { url "https://maven.aliyun.com/repository/public" }
    mavenCentral()
}

group = 'cn.yiiguxing.plugin.translate'
version = "${version}${buildNumber ? '-' + buildNumber : ''}"

intellij {
    version = ideaVersion
    pluginName = 'TranslationPlugin'
    downloadSources = Boolean.valueOf(ideaSources)
    sandboxDirectory 'sandbox'
    plugins "java", "org.jetbrains.kotlin", "Pythonid:203.5981.155", "Dart:203.5981.155", "org.jetbrains.plugins.go:203.5981.155"
}

patchPluginXml {
    sinceBuild customSinceBuild
    untilBuild customUntilBuild
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    compileOnly fileTree(dir: 'libs', include: ['clion.jar', 'rider.jar'])

    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.apache.commons:commons-dbcp2:2.6.0'
    implementation 'commons-dbutils:commons-dbutils:1.7'
    implementation('com.googlecode.soundlibs:mp3spi:1.9.5.4') {
        exclude module: 'junit'
    }
}

sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion

compileKotlin {
    kotlinOptions {
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinTargetVersion
        jvmTarget = javaTargetVersion
    }
}
compileTestKotlin {
    kotlinOptions {
        languageVersion = kotlinLanguageVersion
        apiVersion = kotlinTargetVersion
        jvmTarget = javaTargetVersion
    }
}

runIde {
    systemProperty "idea.auto.reload.plugins", false
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ProcessResources) {
    filesMatching('**/*.properties') {
        filter EscapeUnicode
    }
}
